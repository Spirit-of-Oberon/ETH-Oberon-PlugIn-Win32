(* OBERON System 3, Release 2.2.

Copyright 1996, 1997 Team of the Institute of Computer Systems of ETH Zürich, ETH Center, CH-8092 Zürich,
e-mail: oberon@inf.ethz.ch.

This module may be used under the conditions of the general Oberon System 3 license contract.
The full text can be downloaded from

	"ftp://ftp.inf.ethz.ch/pub/software/Oberon/System3/license.txt;A"

or requested from the above mentioned address.

Under the license terms stated it is in particular (a) prohibited to modify the interface of this module
in any way that disagrees with style or content of the system and (b) requested to provide all conversions
of the source code to another platform with the name OBERON. *)

MODULE Display; (*JG/JM 8.4.94*)

(**
Module Display provides the display drawing primitives and the base type of the visual objects, called Frames.
*)

  IMPORT SYSTEM, Objects, Kernel32, User32, Win32;

  CONST
    BG* = 0; FG* = 15; (** Background, foreground colors. *)

	(** Drawing operation modes. *)
	replace* = 0;	(** replace destination. *)
	paint* = 1;	(** paint over destination.  *)
	invert* = 2;	(** invert destination. *)

    remove* = 0; suspend* = 1; restore* = 2; newprinter* = 3; (** ControlMsg id. *)
    reduce* = 0; extend* = 1; move* = 2; (** ModifyMsg id. *)
    display* = 0; state* = 1; (** ModifyMsg mode. *)
    screen* = 0; printer* = 1; (** DisplayMsg device *)
    full* = 0; area* = 1; contents* = 2; (** DisplayMsg id. *)
    get* = 0; set* = 1; reset* = 2; (** SelectMsg id. *)
    drop* = 0; integrate* = 1; (** ConsumeMsg id. *)

	(* GDI ROP codes *)
	PATCOPY = 0F00021H; PATINVERT = 5A0049H; PATAND = 0A000C9H; PATPAINT = 0FA0089H;
	DSTINVERT = 550009H; SRCINVERT = 660046H; SRCCOPY = 0CC0020H; SRCAND = 8800C6H;
	SRCPAINT = 0EE0086H;

  TYPE
	Pattern* = LONGINT; (* = Win32.PatternPtr *) (*pointer to pattern descriptor*)

    Frame* = POINTER TO FrameDesc;		(** Base type of all displayable objects. *)
    FrameDesc* = RECORD (Objects.ObjDesc)
      next*, dsc*: Frame;	(** Sibling, child pointers. *)
      X*, Y*, W*, H*: INTEGER	(** Coordinates. *)
    END;

    FrameMsg* = RECORD (Objects.ObjMsg)	(** Base type of messages sent to frames. *)
      F*: Frame;	(** Message target, NIL for broadcast. *)
      x*, y*: INTEGER;	(** Message origin. *)
      res*: INTEGER	(** Result code: <0 = error or no response, >=0 response. *)
    END;

    ControlMsg* = RECORD (FrameMsg)
      id*: INTEGER	(** Remove, suspend, restore. *)
    END;

    ModifyMsg* = RECORD (FrameMsg)	(** Change coordinates in container frame. *)
      id*: INTEGER;	(** Reduce, extend, move. *)
      mode*: INTEGER;	(** Modes display, state. *)
      dX*, dY*, dW*, dH*: INTEGER;	(** Change from old coordinates (delta). *)
      X*, Y*, W*, H*: INTEGER	(** New coordinates. *)
    END;

    DisplayMsg* = RECORD (FrameMsg)	(** Display a frame, a part of it or its contents. *)
      device*: INTEGER;	(** screen, printer *)
      id*: INTEGER;	(** full, area, contents. *)
      u*, v*, w*, h*: INTEGER	(** Area to be restored. *)
    END;

    LocateMsg* = RECORD (FrameMsg)	(** Locate frame in display space. *)
      loc*: Frame;	(** Result. *)
      X*, Y*: INTEGER;	(** Absolute location. *)
      u*, v*: INTEGER	(** Relative coordinates in loc. *)
    END;

    SelectMsg* = RECORD (FrameMsg)	(** Selection control. *)
      id*: INTEGER;	(** Get, set, reset. *)
      time*: LONGINT;	(** Time of selection. *)
      sel*: Frame;	(** Parent of selection. *)
      obj*: Objects.Object	(** List of objects involved. *)
    END;

    ConsumeMsg* = RECORD (FrameMsg)	(** Drop, integrate frames. *)
      id*: INTEGER;	(** Drop, integrate. *)
      u*, v*: INTEGER;	(** Relative coordinates in destination when drop. *)
      obj*: Objects.Object	(** List of objects to be consumed *)
    END;

    MsgProc* = PROCEDURE (VAR M: FrameMsg);

TYPE
	COLORREF = Win32.COLORREF;

VAR
  
    Unit*: LONGINT; (** RasterUnit = Unit/36000 mm*)

	Left*,	(** Left margin of black-and-white screen. *)
	ColLeft*,	(** Left margin of color screen, often same as Left. *)
	Bottom*,	(** Bottom of primary map. *)
	UBottom*,	(** Bottom of secondary map. *)
	Width*,	(** Screen width. *)
	Height*: INTEGER;     (** Screen height. *)
	
    arrow*,	(** Oberon cursor. *)
    star*,	(** Star marker to mark documents and viewers. *)
    cross*,	(** Insertion marker. *)
    downArrow*,	(** Marker to indicate disk operation. *)
    hook*: Pattern;	(** Text caret pattern. *)
    
    grey0*, grey1*, grey2*, ticks*, solid* : Pattern;	(** Simulated grey levels. *)

    Broadcast*: MsgProc;	(** Message broadcast to all frames in the display space. *)
    
	clipB, clipL, clipR, clipT: INTEGER;
	
PROCEDURE MakePatterns;
VAR image: ARRAY 33 OF SET; i: INTEGER;
BEGIN
	i := 1;
	WHILE i < 33 DO
		image[i] := {0, 4, 8, 12, 16, 20, 24, 28}; image[i+1] := {};
		image[i+2] := {2, 6, 10, 14, 18, 22, 26, 30}; image[i+3] := {};
		INC(i, 4);
	END;
	Win32.NewPattern(image, 32, 32, grey0);

	i := 1;
	WHILE i < 33 DO
		image[i] := {30, 28, 26, 24, 22, 20, 18, 16, 14, 12, 10, 8, 6, 4, 2, 0}; INC(i);
		image[i] := {31, 29, 27, 25, 23, 21, 19, 17, 15, 13, 11, 9, 7, 5, 3, 1}; INC(i);
	END;
	Win32.NewPattern(image, 32, 32, grey1);

	i := 1;
	WHILE i < 33 DO
		image[i] := {0, 1, 4, 5, 8, 9, 12, 13, 16, 17, 20, 21, 24, 25, 28, 29}; INC(i);
		image[i] := {0, 1, 4, 5, 8, 9, 12, 13, 16, 17, 20, 21, 24, 25, 28, 29}; INC(i);
		image[i] := {2, 3, 6, 7, 10, 11, 14, 15, 18, 19, 22, 23, 26, 27, 30, 31}; INC(i);
		image[i] := {2, 3, 6, 7, 10, 11, 14, 15, 18, 19, 22, 23, 26, 27, 30, 31}; INC(i);
	END;
	Win32.NewPattern(image, 32, 32, grey2);
	
	image[1] := {13}; image[2] := {12..14}; image[3] := {11..13}; image[4] := {10..12};
	image[5] := {9..11}; image[6] := {8..10}; image[7] := {7..9}; image[8] := {0, 6..8};
	image[9] := {0, 1, 5..7}; image[10] := {0..2, 4..6}; image[11] := {0..5}; image[12] := {0..4}; 
	image[13] := {0..5}; image[14] := {0..6}; image[15] := {0..7}; 
	Win32.NewPattern(image, 15, 15, arrow);

	image[1] := {0, 10}; image[2] := {1, 9}; image[3] := {2, 8}; image[4] := {3, 7};
	image[5] := {4, 6}; image[6] := {}; image[7] := {4, 6}; image[8] := {3, 7};
	image[9] := {2, 8}; image[10] := {1, 9}; image[11] := {0, 10};
	Win32.NewPattern(image, 11, 11, cross);

	image[1] := {0..2, 8..11}; image[2] := {0..2, 7..10}; image[3] := {0..2, 6..9}; image[4] := {0..2, 5..8};
	image[5] := {0..2, 4..7}; image[6] := {0..6}; image[7] := {0..5}; image[8] := {0..4};
	image[9] := {0..3}; image[10] := {0..2}; image[11] := {0, 1}; image[12] := {0};
	Win32.NewPattern(image, 12, 12, hook);
		
	(* V4 
	image[1] := {0..7}; image[2] := {0..6}; image[3] := {0..5}; image[4] := {0..4};
	image[5] := {0..3}; image[6] := {0..2}; image[7] := {0..1}; image[8] := {0};
	Win32.NewPattern(image, 8, 8, hook);
	*)
	
	image[1] := {7}; image[2] := {7}; image[3] := {2, 7, 12}; image[4] := {3, 7, 11};
	image[5] := {4, 7, 10}; image[6] := {5, 7, 9}; image[7] := {6..8}; image[8] := {0..6, 8..14};
	image[9] := {6..8}; image[10] := {5, 7, 9}; image[11] := {4, 7, 10}; image[12] := {3, 7, 11};
	image[13] := {2, 7, 12}; image[14] := {7}; image[15] := {7};
	Win32.NewPattern(image, 15, 15, star);

	image[1] := {6}; image[2] := {5..7}; image[3] := {4..8}; image[4] := {3..9};
	image[5] := {2..10}; image[6] := {5..7}; image[7] := {5..7}; image[8] := {5..7};
	image[9] := {5..7}; image[10] := {5..7}; image[11] := {5..7}; image[12] := {5..7};
	image[13] := {5..7}; image[14] := {5..7}; image[15] := {};
	Win32.NewPattern(image, 15, 15, downArrow);

	i := 1;
	WHILE i < 33 DO image[i] := {}; INC(i) END;
	image[1] := {0, 16}; image[17] := {0, 16};
	Win32.NewPattern(image, 32, 32, ticks);

	i := 1;
	WHILE i < 33 DO image[i] := {0..31}; INC(i) END;
	Win32.NewPattern(image, 32, 32, solid);
END MakePatterns;

(** Return address of display screen located at X coordinate. *)
PROCEDURE Map* (X: INTEGER): LONGINT; (* address of map at X*)
BEGIN RETURN 0
END Map;

(** Change color palette entry. 0 <= col, red, green, blue < 256. *)
PROCEDURE SetColor* (col, red, green, blue: INTEGER);
BEGIN
	col := Win32.ColTrans[col];
	Win32.UpdatePalette(col, red, green, blue)
END SetColor;

(** Retrieve color paletter entry. 0 <= col, red, green, blue < 256. *)
PROCEDURE GetColor* (col: INTEGER; VAR red, green, blue: INTEGER);
BEGIN
	col := Win32.ColTrans[col];
	Win32.GetColor(col, red, green, blue)
END GetColor;
    
(** Returns the number of bits per pixel for the given X coordinate. Typical values are 1, 4 and 8. *)
PROCEDURE Depth* (X : INTEGER): INTEGER;
BEGIN
	IF Win32.Depth >= 8 THEN
		RETURN 8
	ELSE
		RETURN SHORT(Win32.Depth)
	END
END Depth;

(** Get the current clip rectangle. *)
PROCEDURE GetClip* (VAR X, Y, W, H: INTEGER);
BEGIN
	X := clipL; Y := clipB; W := clipR- clipL; H := clipT - clipB;
END GetClip;

(** Set the new clipping rectangle. *)
PROCEDURE SetClip* (X, Y, W, H: INTEGER);
BEGIN
	clipL := X; clipB := Y; clipR := X + W; clipT := Y + H;
END SetClip;

(** Intersect with current clip rectangle resulting in a new clip rectangle. *)
PROCEDURE AdjustClip* (X, Y, W, H: INTEGER);
	VAR T, R : INTEGER;
BEGIN
	R := X + W; T := Y + H;
	IF X > clipL THEN clipL := X END; IF Y > clipB THEN clipB := Y END;
	IF R < clipR THEN clipR := R END; IF T < clipT THEN clipT := T END
END AdjustClip;

(** Reset the current clipping rectangle to the whole display. *)
PROCEDURE ResetClip*;
BEGIN
	clipL := 0; clipB := 0; clipR := Width; clipT := Height
END ResetClip;

(** Copy source block SX, SY, W, H to destination DX, DY using operation mode. A block is defined by its lower 
left corner X, Y and its dimension W, H. Some drivers only implement mode = replace. *)
PROCEDURE CopyBlock* (sx, sy, w, h, dx, dy, mode: INTEGER);
	VAR
		ret: LONGINT;
		clip, src, dest, update: User32.Rect;
BEGIN
	Win32.SetClippingArea(0, 0, Win32.DispW, Win32.DispH);
	src.left := sx; src.bottom := Win32.DispH-sy;
	src.right := sx+w; src.top := src.bottom-h;
	clip.left := clipL; clip.right := clipR;
	clip.top := Win32.DispH-clipT; clip.bottom := Win32.DispH-clipB;
	ret := User32.ScrollDC(Win32.hdcDisp, dx-sx, sy-dy, SYSTEM.ADR(src), SYSTEM.ADR(clip), Kernel32.NULL, SYSTEM.ADR(update));
	IF ((update.right-update.left) > w) OR ((update.bottom-update.top) > h) THEN
		dest.left := dx; dest.bottom := Win32.DispH-dy;
		dest.right := dx+w; dest.top := dest.bottom-h;
		ret := User32.IntersectRect(SYSTEM.ADR(clip), SYSTEM.ADR(update), SYSTEM.ADR(dest));
		ret := User32.InvalidateRect(Win32.Display, SYSTEM.ADR(clip), Kernel32.False)
	END
END CopyBlock;

(*
(** Copy source block SX, SY, W, H to destination DX, DY using operation mode. A block is given by its lower left corner X, Y and its dimension W, H.*)
PROCEDURE CopyBlock* (sx, sy, w, h, dx, dy, mode: INTEGER);
VAR u: INTEGER;
	update, clip, scroll: Rect; Dx, Dy: LONGINT;
BEGIN
	IF dx < clipL THEN DEC(w, clipL - dx); INC(sx, clipL - dx); dx := clipL END;
	u := (dx+w)- clipR;
	IF u > 0 THEN DEC(w, u) END;
	IF dy < clipB THEN DEC(h, clipB - dy); INC(sy, clipB - dy); dy := clipB END;
	u := (dy+h) - clipT;
	IF u > 0 THEN DEC(h, u) END;
	IF (h <= 0) OR (w <= 0) THEN RETURN END;
	Win32.SetClippingArea(0, 0, Win32.DispW, Win32.DispH);
	IF Win32.lc.len > 0 THEN Win32.FlushCache END;
	IF (Win32.DispW = Width) & (Win32.DispH = Height) THEN
		Win32.BitBlt(Win32.hdcDisp, dx, Win32.DispH-dy-h, w, h, Win32.hdcDisp, sx, Win32.DispH-sy-h, SRCCOPY);
	ELSIF ((sx # dx) & (Win32.DispW < sx+w)) OR ((sy # dy) & (Height-Win32.DispH > sy)) THEN
		Win32.RefreshDisplay;
	ELSE Win32.BitBlt(Win32.hdcDisp, dx, Win32.DispH-dy-h, w, h, Win32.hdcDisp, sx, Win32.DispH-sy-h, SRCCOPY);
	END
END CopyBlock;
*)

(** Copy pattern pat in color col to X, Y using operation mode. Some drivers do not implement mode = paint. *)
PROCEDURE CopyPattern* (col: INTEGER; pat: Pattern; x, y, mode: INTEGER);
VAR p: Win32.PatternPtr; colref: LONGINT; dmmy: LONGINT; hdcMem: LONGINT;
BEGIN
	Win32.SetClippingArea(clipL, clipB, clipR - clipL, clipT - clipB);
	col := Win32.ColTrans[ORD(SYSTEM.VAL(CHAR, col))];
	(* because the text output routines pass a SHORTINT to CopyPattern which must be regarded as UNSIGNED *)
	
	IF (pat = Win32.cc.pat) & (Win32.cc.font.hfont # 0) & (mode # invert) THEN 
		(* character cache valid and Windows font available. Invert mode not supported by line cache. *)
		Win32.CacheCharacter(x, y, col, mode);
	ELSE
		IF Win32.lc.len > 0 THEN Win32.FlushCache END;
		p := SYSTEM.VAL(Win32.PatternPtr, pat);
		colref := Win32.PaletteIndex(col);
		hdcMem := Win32.CreateCompatibleDC(Win32.hdcDisp);
		dmmy := Win32.SelectObject(hdcMem, p.bitmap);
		CASE mode OF
			| invert:
				Win32.SetBackgCol(Win32.White); Win32.SetTextCol(Win32.Black);
				Win32.BitBlt(Win32.hdcDisp, x, Win32.DispH-(y+p.h), p.w, p.h, hdcMem, p.x, p.y, SRCINVERT)
			| replace:
				Win32.SetBackgCol(colref); Win32.SetTextCol(Win32.Backg);
				Win32.BitBlt(Win32.hdcDisp, x, Win32.DispH-(y+p.h), p.w, p.h, hdcMem, p.x, p.y, SRCCOPY);
			| paint:
				IF colref # Win32.White THEN
					Win32.SetBackgCol(Win32.Black); Win32.SetTextCol(Win32.White);
					Win32.BitBlt(Win32.hdcDisp, x, Win32.DispH-(y+p.h), p.w, p.h, hdcMem, p.x, p.y, SRCAND);
				END;
				IF colref # Win32.Black THEN
					Win32.SetBackgCol(colref); Win32.SetTextCol(Win32.Black);
					Win32.BitBlt(Win32.hdcDisp, x, Win32.DispH-(y+p.h), p.w, p.h, hdcMem, p.x, p.y, SRCPAINT);
				END;
		ELSE 
		END;
		Win32.DeleteDC(hdcMem);
	END
END CopyPattern;
    
(** Replicate pattern pat in color col into block X, Y, W, H using operation mode, proceeding from left to right 
and from bottom to top, starting at lower left corner. The pattern origin is placed at pX, pY. Some drivers do 
not implement mode = paint. *)
PROCEDURE FillPattern* (col: INTEGER; pat: Pattern; pX, pY, x, y, w, h, mode: INTEGER);
VAR u: INTEGER; ret, colref: LONGINT;
BEGIN
	IF x < clipL THEN DEC(w, clipL - x); x := clipL END;
	u := (x+w)-clipR;
	IF u > 0 THEN DEC(w, u) END;
	IF y < clipB THEN DEC(h, clipB - y); y := clipB END;
	u := (y+h) - clipT;
	IF u > 0 THEN DEC(h, u) END;
	IF (h <= 0) OR (w <= 0) THEN RETURN END;
	Win32.SetClippingArea(0, 0, Win32.DispW, Win32.DispH);
	IF Win32.lc.len > 0 THEN Win32.FlushCache END;
	Win32.SetPatternBrush(SYSTEM.VAL(Win32.PatternPtr, pat));
	Win32.SetBrushOrgEx(Win32.hdcDisp, pX, Win32.DispH-pY, 0);
	col := Win32.ColTrans[col];
	colref := Win32.PaletteIndex(col);
	CASE mode OF
		| invert:
			Win32.SetBackgCol(Win32.White); Win32.SetTextCol(Win32.Black);
			ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, PATINVERT)
		| replace:
			Win32.SetBackgCol(colref); Win32.SetTextCol(Win32.Backg);
			ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, PATCOPY)
		| paint:
			IF colref # Win32.White THEN
				Win32.SetBackgCol(Win32.Black); Win32.SetTextCol(Win32.White);
				ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, PATAND)
			END;
			IF colref # Win32.Black THEN
				Win32.SetBackgCol(colref); Win32.SetTextCol(Win32.Black);
				ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, PATPAINT)
			END
	END
END FillPattern;

(** Replicate pattern pat in color col into block X, Y, W, H using operation mode, proceeding from left to right 
and from bottom to top, starting at lower left corner. The pattern pin-point is at 0, 0. Some drivers do not 
implement mode = paint. *)
PROCEDURE ReplPattern* (col: INTEGER; pat: Pattern; X, Y, W, H, mode: INTEGER);
BEGIN
	FillPattern(col,pat,0,0,X, Y, W, H, mode);
END ReplPattern;

(** Block fill in color col and operation mode. Some drivers do not implement mode = paint. *)
PROCEDURE ReplConst* (col: INTEGER; x, y, w, h, mode: INTEGER);
	VAR u: INTEGER; colref: COLORREF; ret: LONGINT;
BEGIN
	IF x < clipL THEN DEC(w, clipL - x); x := clipL END;
	u := (x+w)-clipR;
	IF u > 0 THEN DEC(w, u) END;
	IF y < clipB THEN DEC(h, clipB - y); y := clipB END;
	u := (y+h) - clipT;
	IF u > 0 THEN DEC(h, u) END;
	IF (w <= 0) OR (h <= 0) THEN RETURN END;
	Win32.SetClippingArea(0, 0, Win32.DispW, Win32.DispH);
	IF Win32.lc.len > 0 THEN Win32.FlushCache END;
	IF mode = invert THEN
		(*rect.left := x; rect.right := x+w;
		rect.bottom := Win32.DispH-y; rect.top := rect.bottom-h;
		ret := User32.InvertRect(Win32.hdcDisp, SYSTEM.ADR(rect))*)
		ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y, w, -h, DSTINVERT)
	ELSIF (col = 0) & (mode >=0) THEN ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, Win32.BackgRop)
	ELSIF (col = 15) & (mode >=0)  THEN ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, Win32.ForegRop)
	ELSE
		IF mode >= 0 THEN col := Win32.ColTrans[col] END;
		colref := Win32.PaletteIndex(col);
		Win32.SetBrushColor(colref);
		ret := Win32.PatBlt(Win32.hdcDisp, x, Win32.DispH-y-h, w, h, PATCOPY)
	END
END ReplConst;
    
(** Place a dot of color col in operation mode at x, y. Effect equivalent to ReplConst with a block of size 1, 1. *)
PROCEDURE Dot* (col, x, y, mode: INTEGER);
BEGIN ReplConst(col, x, y, 1, 1, mode);
END Dot;

(** Returns the dimensions of a pattern. *)
PROCEDURE GetDim* (pat: Pattern; VAR W, H: INTEGER);
VAR p: Win32.PatternPtr;
BEGIN
	p := SYSTEM.VAL(Win32.PatternPtr, pat); W := p.w; H := p.h;
END GetDim;

(** Define a new pattern. *)
PROCEDURE NewPattern* (w, h: INTEGER; VAR image: ARRAY OF SET): Pattern;
VAR pat: LONGINT; buf: ARRAY 128 OF SET; i: INTEGER;
BEGIN
	i := 0;
	WHILE (i < LEN(image)) & (i < LEN(buf) - 1) DO buf[i + 1] := image[i]; INC(i) END; buf[0] := {};
	Win32.NewPattern(buf, w, h, pat);
	RETURN pat;
END NewPattern;

BEGIN
	Left := 0; ColLeft := Left;
	Bottom := 0; UBottom := Bottom;
	Width := SHORT(Win32.DispW); Height := SHORT(Win32.DispH);
	Unit := 10000; MakePatterns; ResetClip
END Display.

(** Remarks:

1. Background and Foreground colors
Oberon can either be used with white text on a black background, or inverted with black text on a white background (the so-called paper model). To reduce confusion you should use the FG and BG constants in your code. The black and white constants are only there for compatibility with older applications and do not reflect true white and black. It is recommended to use the paper model for all Oberon applications. Oberon uses color palettes and does not directly support true color displays (although it might be possible to use Oberon on such a display).

2. Screen Organization and Depth
Oberon supports multiple displays that are placed next to each other in increasing X coordinates. The X coordinate thus uniquely determines the screen, hence the single parameter of the Depth procedure. Each screen contains a set of nested frames. Historically, the first screen (at Left) is the black and white screen, and the second screen is the color screen (at ColLeft). Today Left and ColLeft are typically set to 0, as only a few computers still use black and white displays. The screen origin is located at the bottom-left corner of the display (i.e. Y decrease from the top to the bottom of the display).

3. The Frames and the Frame messages
Frames are the visual entities of Oberon. The frames are placed in the display space to make them visible. This involves inserting frames using the dsc and next fields into a container already located in the display space. All frames of a container are linked together in priority sequence using the next field. The dsc field points to the first child frame of a container. Note that this is only a convention and certain frames might decide to manage their contents in different ways. The frame messages are used to manipulate Frames (remember that frames also respond to the object message defined in module Objects).

4. Target or Destination frames
The Frame messages are often broadcasted into the display space rather than sending them directly to a frame. This is useful when many frames are to be informed of an event. It is also a means to determine the exact location (in coordinates and nesting) of a frame in the display space by following the message thread from the display root object to the frame itself (see module Objects). It is also possible to address a broadcasted message to a certain frame. This is called a directed broadcast. The target or destination of a broadcasted message is identified by the F field in the FrameMsg. It is set to NIL when all frames are to receive the message (true broadcast) and to a specific frame if only that frame is interested in the message (directed broadcast).

5. Frame Coordinates and Message origin
The coordinates of a frame are specified relative to its container or parent frame. The frame messages pass the absolute position of the top-left corner of the parent frame to their children in the x and y fields (called the message origin). Thus the absolute display position of a frame F is determined when it receives a frame message M:

	M.x + F.X, M.y + F.Y, F.W, F.H
	
6. Invalidating Messages
Often a frame knows that a message broadcast need not pass through the remainder of display space because it has already been handled. In such cases the res field of the frame message is set to zero or positive to indicate an action has been completed and that the message should be regarded as invalidated. Setting the res field in this way is called invalidating a message and will terminate the message broadcast.

7. Broadcasting Frame Messages
The Broadcast procedure broadcasts a message through the display space. The procedure initializes fields in the frame message. The message origin is set to (0, 0), the message is time stamped, the res field is set to a negative value, and the dlink field is initialized to NIL. In addition, the clipping rectangle is set to the whole display area.

8. Clipping
To prevent the clipping rectangle clipping the wrong display primitives, it is reset on each message broadcast or when a trap occurs. Clipping does not affect the drawing of cursors. The Gadget system uses "display masks" that hide the management of the clipping rectangle from the programmer.

9. NewPattern
The NewPattern procedure allows you to define patterns in a machine portable way. The sets contain 32 bits, each specifying a single pixel, with {0} the left-most pixel of the pattern. Each pattern line must be filled with empty bits so that it is a multiple of 32 pixels long. The first SET of the array contains the bottom line of the pattern.

10. ControlMsg
The ControlMsg is primarily used to remove a frame from the display space. When id is set to remove, the destination frame should be removed from its current location. This done by the container the frame is contained in (and is thus a slight misinterpretation of F as a destination frame). More than one frame are removed if the destination is a list of frames (connected by the slink field). In this case, they should at all belong to the same parent. This message should always be broadcast.
When the message id is set to suspend or restore it indicates that all frames from the destination downwards in the display space will be temporarily removed from the display space, or will be restored to the display space. This allow frames to update their internal data structures as they may have missed messages while suspended (this is because only those frames located in the display space receive message broadcasts). This message is sent to the top-most frame of that part of the display space involved (with the destination set to NIL).

11. ModifyMsg
The ModifyMsg broadcasts resize request to the destination frame. This allows you to move the relative position or change the size of a child in a container. When the mode is set to state, the frame should not display itself immediately but only update its size and position (possibly indicating changes to its children in turn). It is then the task of the sender to send a follow up Display message to the frame. The latter way of using the ModifyMsg allows a container to influence its children without having them draw themselves for each change made. This message must never be invalidated (i.e. it must travel throughout the whole display space). The dX, dY, dW, dH coordinates should always be set correctly to indicate the change in position and size from the original position and size. The id field of the ModifyMsg is ignored by most frames in the system.

12. DisplayMsg
The DisplayMsg sends either a redraw request or a print request to a destination frame, depending on the value of device (screen or printer). When the destination is NIL, a whole DAG of gadgets are implied. When id is set to area, the area u, v, w, h inside the destination frame should be redrawn. Gadgets assume that these coordinates are relative to the top left-most corner of the destination gadget (thus v is negative).
When printing, the x, y coordinates indicate the absolute printer coordinates of the left-bottom corner of the frame on paper (and not the left-bottom corner of the container). When the id is set to full, the frame has to print itself in the form it looks on the display. When the id is set to contents it should print its complete contents (for example a multi-page text that it is displaying). The frame may assume that the printer driver has been initialized already. Readying the printer is the task of the sender, and is done by calling Printer.Open.

13. LocateMsg
This message is broadcast to locate the frame positioned at the absolute coordinates X, Y on the display. The result, if any, is found in the loc field. The frame should return the relative position u, v of X, Y inside itself. Gadgets return these coordinates relative to their top-left corner (i.e. v is typically negative). By convention, the message is invalidated when the loc field is set.

14. SelectMsg
When id is set to get, the message is used to return the current object selection (a list) in obj. The parent of the selected objects is returned in the sel field. This message is broadcast with the destination set to NIL. The time of the selection is returned in the time field. Each container frame in the display space typically compares the time of its selection with the time field in the message, updating the returned selection when it is after the time set in the message field. When id is set to set or reset, the destination frame should select or unselect itself. It should never draw itself at this point. This is the task of the message sender.

15. ConsumeMsg
When the id is set to drop, the destination frame is requested to consume the list of objects found in the obj field. This is the way how objects can be added dynamically to a container. The relative u, v coordinates indicate where inside of this gadget (note that v is typically negative). When id is set to integrate, the message is broadcast to all and indicates that the frame owning the focus (caret) should consume the list of objects.

16. The Broadcast procedure is installed by module Viewers to the default message broadcasting procedure found it that module.


*)
